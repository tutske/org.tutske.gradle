plugins {
	id 'java-gradle-plugin'
	id 'groovy'
	id 'maven-publish'
}

group = 'org.tutske.gradle'
version = 'git describe --dirty'.execute ().text.trim ();

gradlePlugin {
	plugins {
		tg {
			id = 'org.tutske.gradle'
			implementationClass = 'org.tutske.gradle.TgPlugin'
		}
	}
}

publishing {
	repositories {
		def repoUrl = project.hasProperty ('url') ? url : 'https://nexus.tutske.org:10443/repository'
		def repoName = (
			project.hasProperty ('repo') ? repo :
			project.version =~ /.*-dirty$/ ? 'maven-dirties' :
			project.version =~ /.*(-pre-[0-9]*)?-g[A-Fa-f0-9]{7,}/ ? 'maven-pre-releases' :
			'maven-releases'
		)

		def principal = project.hasProperty ('username') ? username : 'gradle'
		def pass = project.hasProperty ('password') ? password : 'gradle'

		maven {
			url "${repoUrl}/${repoName}"
			credentials { username principal; password pass; }
		}
	}
}

tasks.whenTaskAdded { task ->
	if ( task.name.contains ('publishTgPluginMarkerMavenPublicationToMavenRepository') ) {
		task.enabled = false
	}
}
